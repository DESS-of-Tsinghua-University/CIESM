#!/bin/csh -f

#################################################################################
if ($PHASE == set_batch) then
#################################################################################

source ./Tools/ccsm_getenv || exit -1

set ntasks  = `${CASEROOT}/Tools/taskmaker.pl -sumonly`
set maxthrds = `${CASEROOT}/Tools/taskmaker.pl -maxthrds`
@ nodes = $ntasks / ${MAX_TASKS_PER_NODE}
if ( $ntasks % ${MAX_TASKS_PER_NODE} > 0) then
  @ nodes = $nodes + 1
  @ ntasks = $nodes * ${MAX_TASKS_PER_NODE}
endif
@ taskpernode = ${MAX_TASKS_PER_NODE} / ${maxthrds}
set qname = q_x86_cn_cess 
set tlimit = "168:00:00"
if ($CCSM_ESTCOST > 2) set tlimit = "168:00:00"

#--- Job name is first fifteen characters of case name ---
set jobname = `echo ${CASE} | cut -c1-15`

if ($?TESTMODE) then
 set file = $CASEROOT/${CASE}.test 
else
 set file = $CASEROOT/${CASE}.run 
endif

cat >! $file << EOF1
#!/bin/csh -f
##SBATCH --job-name=${jobname}
##SBATCH --time=$tlimit
##SBATCH --nodes=$nodes
###SBATCH --ntasks=$ntasks
##SBATCH --cpus-per-task=$maxthrds
##SBATCH --exclusive
####SBATCH --mail-type=END
####SBATCH --mail-user=xyz@abc.com
##SBATCH --output=slurm.out

EOF1

#################################################################################
else if ($PHASE == set_exe) then
#################################################################################

source ./Tools/ccsm_getenv || exit -1
set maxthrds = `${CASEROOT}/Tools/taskmaker.pl -maxthrds`
set maxtasks = `${CASEROOT}/Tools/taskmaker.pl -sumtasks`

set jobname = `echo ${CASE} | cut -c1-15`
set qname = q_x86_cn_cess 

cat >> ${CASEROOT}/${CASE}.run << EOF1
# -------------------------------------------------------------------------
# # Run the model
# # -------------------------------------------------------------------------
sleep 5
cd \$RUNDIR
echo "\`date\` -- CSM EXECUTION BEGINS HERE" 

set jobinfo = \`bsub -b -m 1 -p -q $qname -J $jobname -n $maxtasks -o cesm.log.\$LID $EXEROOT/cesm.exe\`
set jobid = \`echo \$jobinfo |awk -F '[<>]' '{print \$2}'\`

while(1)
  set jobstatinfo = \`bjobs -l \$jobid\`
  set jobstat = \`echo \$jobstatinfo |awk -F '[<>]' '{print \$6}'\`
  if (\$jobstat == "RUN" || \$jobstat == "PEND" || \$jobstat == "STARTING") then
     sleep 10
  else if (\$jobstat == "EXIT" || \$jobstat == "KILLING") then
     exit -1
  else if (\$jobstat == "DONE") then
     break
  else
     exit -1
  endif
end

wait
echo "\`date\` -- CSM EXECUTION HAS FINISHED" 

EOF1


#################################################################################
else if ($PHASE == set_larch) then
#################################################################################

   #This is a place holder for a long-term archiving script

#################################################################################
else
#################################################################################

    echo "  PHASE setting of $PHASE is not an accepted value"
    echo "  accepted values are set_batch, set_exe and set_larch"
    exit 1

#################################################################################
endif
#################################################################################

